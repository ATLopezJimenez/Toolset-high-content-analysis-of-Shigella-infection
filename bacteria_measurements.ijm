// ImageJ macro by Ana Teresa Lopez Jimenez @ LSHTM

// This macro has been developped and used in the pre-print: 
// High-content superresolution microscopy and deep learning assisted analysis reveals host and bacterial heterogeneity during Shigella infection. Ana T. López-Jiménez, Dominik Brokatzky, Kamla Pillay, Tyrese Williams, Gizem Özbaykal Güler and Serge Mostowy (2024)
 
//--------------------DESCRIPTION--------------------//
// This ImageJ macro measures fluorescence intensity of the bacteria in a channel determined by the user.
// This ImageJ macro uses as input the square crop images generated by the ImageJ macro "Segmentation_bacteria_square_crops.ijm"



//--------------------Selecting the input folder, output folder and image format to be analysed--------------------//

#@ File (label = "Input directory", style = "directory") input
#@ File (label = "Output directory", style = "directory") output
#@ String (label = "File suffix", value = ".tif") suffix





//--------------------Obtaining information about the image--------------------//

Dialog.create("Provide this information");
Dialog.addString("Channel to be measured (1, 2, 3, 4..?)", "1");
Dialog.show();
cbact=Dialog.getString();
newcbact="C" + cbact;
print("Channel being measured: " + newcbact);





//--------------------Setting up batch mode to analyse multiple images in the same folder--------------------//

setBatchMode(false)

processFolder(input);

function processFolder(input) {
	list = getFileList(input);
	list = Array.sort(list);
	for (i = 0; i < list.length; i++) {
		if(File.isDirectory(input + File.separator + list[i]))
			processFolder(input + File.separator + list[i]);
		if(endsWith(list[i], suffix))
			processFile(input, input, list[i]);
	}
}

function processFile(input, output, file) {
open(input + File.separator + file);





//--------------------Retrieving further information about the image--------------------//

dir = getDirectory("image");
name = getTitle();
imagebact= newcbact + "-" + name;
Stack.getDimensions(width, height, channels, slices, frames);
chan = channels;
cmask=chan;
newcmask="C" + cmask;
imagemask= newcmask + "-" + name;





//--------------------Image analysis--------------------//

run("Split Channels");
selectWindow(imagemask);
run("Set Measurements...", "area mean standard shape integrated median area_fraction stack display redirect=["+ imagemask +"] decimal=3");
setOption("BlackBackground", true);
run("Convert to Mask");
run("Analyze Particles...", "size=0-50 circularity=0.00-1.00 exclude add");
run("Set Measurements...", "area mean standard shape integrated median area_fraction stack display redirect=["+ imagebact +"] decimal=3");
roiManager("Measure"); 
roiManager("Delete");
close(".tif");
}

saveAs("Results", output + File.separator + "Results_bacteria_measurements.csv");
print("Measurements clickIT saved successfully");
//run("Clear Results");
close("*");




